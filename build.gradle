// Gradle script to build the Macana project

plugins {
    id 'application' // to build JVM applications
    alias(libs.plugins.download)
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

ext {
    // which BTF (buildType + flavor) of the native physics library to copy:
    btf = 'ReleaseSp'
    //btf = 'DebugSp'

    fs = System.getProperty('file.separator')
    downloadsDir = System.getProperty('user.home') + fs + 'Downloads' + fs
    lbjVersion = libs.versions.libbulletjme.get()

    // URL from which native physics libraries should be copied:
    libbulletjmeUrl = "https://github.com/stephengold/Libbulletjme/releases/download/$lbjVersion/"
    //libbulletjmeUrl = "file:///home/sgold/NetBeansProjects/Libbulletjme/dist/"
}

tasks.withType(JavaCompile).configureEach { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
}

// Register tasks to run specific applications:

tasks.register('HelloObsidian', JavaExec) {
    description 'Runs the HelloObsidian app.'
    mainClass = 'com.github.stephengold.macana.HelloObsidian'
}
tasks.register('rdHelloObsidian', Exec) {
    commandLine = [
            '/usr/share/renderdoc_1.31/bin/renderdoccmd',
            'capture',
            '--wait-for-exit',
            '--working-dir', '.',
            "${System.getProperty('java.home')}/bin/java",
            '-classpath', sourceSets.main.runtimeClasspath.asPath,
            'com.github.stephengold.macana.HelloObsidian'
            ]

    dependsOn = ['classes', 'downloadNatives']
    description 'Runs HelloObsidian with a renderDoc overlay.'
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
OperatingSystem os = DefaultNativePlatform.currentOperatingSystem

tasks.withType(JavaExec).configureEach { // Java runtime options:
    if (os.isMacOsX()) {
        jvmArgs '-XstartOnFirstThread' // required for GLFW on macOS
    }
    classpath sourceSets.main.runtimeClasspath
    dependsOn 'downloadNatives'
    enableAssertions true
}

Boolean includeLinux = os.isLinux()
Boolean includeMacOsX = os.isMacOsX()
Boolean includeWindows = os.isWindows()
tasks.register('downloadNatives') {
    if (includeLinux) {
        dependsOn 'downloadLinux64'
    }
    if (includeMacOsX) {
        dependsOn 'downloadMacOSX64'
        dependsOn 'downloadMacOSX_ARM64'
    }
    if (includeWindows) {
        dependsOn 'downloadWindows64'
    }
}

application {
    mainClass = 'com.github.stephengold.macana.HelloObsidian'
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of snapshots
}

dependencies {
    implementation(libs.sport)
    implementation(libs.libbulletjme)

    implementation(libs.obsidian)
    implementation(libs.skija.shared)

    implementation(libs.joml)
    implementation(libs.lwjgl)
    implementation(libs.lwjgl.assimp)
    implementation(libs.lwjgl.glfw)
    implementation(libs.lwjgl.opengl)
    implementation(libs.lwjgl.yoga)

    implementation platform(libs.lwjgl.bom)

    if (includeLinux) {
        runtimeOnly(libs.skija.linux.x64)
        runtimeOnly(variantOf(libs.lwjgl){classifier("natives-linux")})
        runtimeOnly(variantOf(libs.lwjgl.assimp){classifier("natives-linux")})
        runtimeOnly(variantOf(libs.lwjgl.glfw){classifier("natives-linux")})
        runtimeOnly(variantOf(libs.lwjgl.opengl){classifier("natives-linux")})
        runtimeOnly(variantOf(libs.lwjgl.yoga){classifier("natives-linux")})
    }

    if (includeMacOsX) {
        runtimeOnly(libs.skija.macos.x64)
        runtimeOnly(variantOf(libs.lwjgl){classifier("natives-macos")})
        runtimeOnly(variantOf(libs.lwjgl.assimp){classifier("natives-macos")})
        runtimeOnly(variantOf(libs.lwjgl.glfw){classifier("natives-macos")})
        runtimeOnly(variantOf(libs.lwjgl.opengl){classifier("natives-macos")})
        runtimeOnly(variantOf(libs.lwjgl.yoga){classifier("natives-macos")})

        runtimeOnly(libs.skija.macos.arm64)
        runtimeOnly(variantOf(libs.lwjgl){classifier("natives-macos-arm64")})
        runtimeOnly(variantOf(libs.lwjgl.assimp){classifier("natives-macos-arm64")})
        runtimeOnly(variantOf(libs.lwjgl.glfw){classifier("natives-macos-arm64")})
        runtimeOnly(variantOf(libs.lwjgl.opengl){classifier("natives-macos-arm64")})
        runtimeOnly(variantOf(libs.lwjgl.yoga){classifier("natives-macos-arm64")})
    }

    if (includeWindows) {
        runtimeOnly(libs.skija.windows.x64)
        runtimeOnly(variantOf(libs.lwjgl){classifier("natives-windows")})
        runtimeOnly(variantOf(libs.lwjgl.assimp){classifier("natives-windows")})
        runtimeOnly(variantOf(libs.lwjgl.glfw){classifier("natives-windows")})
        runtimeOnly(variantOf(libs.lwjgl.opengl){classifier("natives-windows")})
        runtimeOnly(variantOf(libs.lwjgl.yoga){classifier("natives-windows")})
    }
}

// Register tasks to download/clean the native physics library for each platform:

registerPlatformTasks('Linux64',      '_libbulletjme.so')
registerPlatformTasks('MacOSX64',     '_libbulletjme.dylib')
registerPlatformTasks('MacOSX_ARM64', '_libbulletjme.dylib')
registerPlatformTasks('Windows64',    '_bulletjme.dll')

// helper method to register 'download' and 'clean' tasks:

void registerPlatformTasks(String platform, String suffix) {
    String filename = platform + btf + suffix

    String cleanTaskName = 'clean' + platform
    clean.dependsOn(cleanTaskName)
    tasks.register(cleanTaskName, Delete) {
        delete downloadsDir + filename
    }

    tasks.register('download' + platform, Download) {
        src libbulletjmeUrl + filename
        dest file(downloadsDir + filename)
    }
}
